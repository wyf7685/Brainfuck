# 遍历输出 $0 内的素数

>>+++++[<++++[<+++++>-]>-]<<--  # $0=98
# ---- # $0=252
>+<  # $1=1

[- # 循环$0次
  >+<  # $1++
  >>[-]+<<  # $2=true (isPrime)

  ## for ($4=2; $3=($4<$1); $4++)
  >>>+<<<  # $3=true
  >>>>[-]++<<<<  # $4=2
  >>>[  # while $3
    [-]<<<  # $3=false

    > [->>>>+>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <  # 复制$1到$5 : $6
    >>>>  ## 判断 $4<$5 开始
      # 缩进内$+4
      [->>+>+<<<]>>>[-<<<+>>>]<<<  # 复制$0到$2
      > [->>+>+<<<]>>>[-<<<+>>>]<<< <  # 复制$1到$3
      >>>>[-]>[-]+>[-]>[-]<<<<<<<  # $4=0, $5=1, $6=0, $7=0
      >>>>> [  # while $5
        [-] <<<<<  # $5=0

        >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
        >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0

        >>>>>>>[- <<<<<<< # while $7
          ## if $3: $5=$6=1
          >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
          >>>>[  # if $4
            [-] # $4=0
            >[-]+  # $5=1
            >[-]+  # $6=1
          <<] <<<< # 判断复位$4

          ## if $2 && $3: $2--, $3-- 
          >>>>>>[  # if $6
            [-]  # $6=0
            <<<-  # $3--
            <-  # $2--
          >>>>] <<<<<< # 判断复位$6

          >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
          >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0
          
          >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
          >>>>>>>>+<<<<[[-]>>>>-<<<<]<<<<  # $8=!$4, $4=0
          >>>>>>>> [[-]<[-]>] <<<<<<<<  # if $8: $7=$8=0

        >>>>>>>] <<<<<<<  # 循环复位$7
      >>>>>] <<<<<  # 循环复位$5

      >>>>[-]>[-]>[-]>[-]>[-]<<<<<<<<  # $4=$5=$6=$7=$8=0
      >>[-]>[[-]<+>]<<<  # 输出: $2 = ($0<$1)
      >[-]>[-<+>]<<  # $1=$2, $2=0
    <<<<  ## 判断 $4<$5 结束, 结果为 $5
    >>>>>[-<<+>>]<<<<<  # $3=$5, $5=0

    ## 取余运算
    > [->>>>+>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <  # 复制$1到$5 : $6
    >>>> [->>+>+<<<]>>>[-<<<+>>>]<<< <<<<  # 复制$4到$6 : $7
    >>>>>  ## 计算 $5 % $6 开始
      # 缩进内 $+5
      # 计算 $0 % $1
      >>+<<  # $2=true
      >>[  # while $2
        [-]<<  # $2=false

        [->>>+>+<<<<]>>>>[-<<<<+>>>>]<<<<  # 复制$0到$3 : $4
        > [->>>+>+<<<<]>>>>[-<<<<+>>>>]<<<< <  # 复制$1到$4 : $5

        >>> # 判断 $3<$4 开始
          # 缩进内$+3
          [->>+>+<<<]>>>[-<<<+>>>]<<<  # 复制$0到$2
          > [->>+>+<<<]>>>[-<<<+>>>]<<< <  # 复制$1到$3
          >>>>[-]>[-]+>[-]>[-]<<<<<<<  # $4=0, $5=1, $6=0, $7=0
          >>>>> [  # while $5
          [-] <<<<<  # $5=0
          
          >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
          >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0
          
          >>>>>>>[- <<<<<<< # while $7
              ## if $3: $5=$6=1
              >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
              >>>>[  # if $4
              [-] # $4=0
              >[-]+  # $5=1
              >[-]+  # $6=1
              <<] <<<< # 判断复位$4
          
              ## if $2 && $3: $2--, $3-- 
              >>>>>>[  # if $6
              [-]  # $6=0
              <<<-  # $3--
              <-  # $2--
              >>>>] <<<<<< # 判断复位$6
          
              >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
              >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0
              
              >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
              >>>>>>>>+<<<<[[-]>>>>-<<<<]<<<<  # $8=!$4, $4=0
              >>>>>>>> [[-]<[-]>] <<<<<<<<  # if $8: $7=$8=0
          
          >>>>>>>] <<<<<<<  # 循环复位$7
          >>>>>] <<<<<  # 循环复位$5
          
          >>>>[-]>[-]>[-]>[-]>[-]<<<<<<<<  # $4=$5=$6=$7=$8=0
          >>[-]>[[-]<+>]<<<  # 输出: $2 = ($0<$1)
          
          # 处理返回值
          [-]>[-]<  # $0=$1=0
        >>[-<<<+>>>]<<  # 判断结果移动到 $2
        <<<  # 判断 $3<$4 结束

        >>>+<[[-]>-<]<<  # $3=!$2=($0>=$1), $2=0
        >>> [->+<<+>]>[-<+>]< <<<  # 复制$3到$2 : $4

        >>>[  # if $3 ($0>=$1)
          [-]<<<  # $3=false
          > [->>>+>+<<<<]>>>>[-<<<<+>>>>]<<<< <  # 复制$1到$4 : $5
          >>>>[-<<<<->>>>]<<<<  # $0-=$4, $4=0
        >>>] <<<  # 判断复位$3

      >>] <<  # 循环复位$2
      >[-]<  # $1=0, 取余结果为$0
    <<<<<  ## 计算 $5 % $6 结束, 结果为 $5

    ## $6: 标记 $1 是否为素数
    ## if $6=($5 == 0 && $3) $3=$2=0
    >>>>>> [-]+<[[-]>-<] <<<<<  # $6 = ($5==0), $5=0
    >>> [->>+>>+<<<<]>>>>[-<<<<+>>>>]<<<< <<<  # 复制$3到$5 : $7
    >>>>>>> [-]+<<[[-]>>-<<] <<<<<  # $7 = !$5, $5=0
    >>>>>>> [[-]<[-]>] <<<<<<<  # $6 = ($6 && !$7), $7=0

    >>>>>>[  # if $6
      [-]<<<<<<  # $6=false
      >>[-]>[-]<<<  # $3=false, $2=false
    >>>>>>] <<<<<<  # 判断复位$6

    >>>>+<<<<  # $4++
  >>>] <<<  # 循环复位$3

  >>>[-]>[-]>[-]>[-]<<<<<<  # $(3-6)=0

  >>[  # if $2 (isPrime)
    [-]<<  # $2=false
    > [->>+>+<<<]>>>[-<<<+>>>]<<< <  # 复制$1到$3 : $4
    >>> # 输出数值 $3 开始
      # 缩进内 $+1
      ## 输出3位数
      ## $1 存入百位
      [->+>+>+<<<]>>>[-<<<+>>>]<<<  # 复制$0到$1$2 : $3
      >>> >>+++++[<++++[<+++++>-]>-]<< <<<  # $3=100

      >> ## 计算 $2 % $3 开始
        # 缩进内 $+2
        # 计算 $0 % $1
        >>+<<  # $2=true
        >>[  # while $2
          [-]<<  # $2=false

          [->>>+>+<<<<]>>>>[-<<<<+>>>>]<<<<  # 复制$0到$3 : $4
          > [->>>+>+<<<<]>>>>[-<<<<+>>>>]<<<< <  # 复制$1到$4 : $5

          >>> # 判断 $3<$4 开始
            # 缩进内$+3
            [->>+>+<<<]>>>[-<<<+>>>]<<<  # 复制$0到$2
            > [->>+>+<<<]>>>[-<<<+>>>]<<< <  # 复制$1到$3
            >>>>[-]>[-]+>[-]>[-]<<<<<<<  # $4=0, $5=1, $6=0, $7=0
            >>>>> [  # while $5
            [-] <<<<<  # $5=0
            
            >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
            >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0
            
            >>>>>>>[- <<<<<<< # while $7
                ## if $3: $5=$6=1
                >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
                >>>>[  # if $4
                [-] # $4=0
                >[-]+  # $5=1
                >[-]+  # $6=1
                <<] <<<< # 判断复位$4
            
                ## if $2 && $3: $2--, $3-- 
                >>>>>>[  # if $6
                [-]  # $6=0
                <<<-  # $3--
                <-  # $2--
                >>>>] <<<<<< # 判断复位$6
            
                >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
                >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0
                
                >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
                >>>>>>>>+<<<<[[-]>>>>-<<<<]<<<<  # $8=!$4, $4=0
                >>>>>>>> [[-]<[-]>] <<<<<<<<  # if $8: $7=$8=0
            
            >>>>>>>] <<<<<<<  # 循环复位$7
            >>>>>] <<<<<  # 循环复位$5
            
            >>>>[-]>[-]>[-]>[-]>[-]<<<<<<<<  # $4=$5=$6=$7=$8=0
            >>[-]>[[-]<+>]<<<  # 输出: $2 = ($0<$1)
            
            # 处理返回值
            [-]>[-]<  # $0=$1=0
          >>[-<<<+>>>]<<  # 判断结果移动到 $2
          <<<  # 判断 $3<$4 结束

          >>>+<[[-]>-<]<<  # $3=!$2=($0>=$1), $2=0
          >>> [->+<<+>]>[-<+>]< <<<  # 复制$3到$2 : $4

          >>>[  # if $3 ($0>=$1)
            [-]<<<  # $3=false
            > [->>>+>+<<<<]>>>>[-<<<<+>>>>]<<<< <  # 复制$1到$4 : $5
            >>>>[-<<<<->>>>]<<<<  # $0-=$4, $4=0
          >>>] <<<  # 判断复位$3

        >>] <<  # 循环复位$2
        >[-]<  # $1=0, 取余结果为$0
      << ## 计算 $2 % $3 结束, 结果为 $2

      >> [-<->] <<  # $1-=$2, $2=0
      >> >>+++++[<++++[<+++++>-]>-]<< <<  # $2=100

      > ## 计算 $1 / $2 开始
        # 缩进内 $+1
        # 计算 $0 / $1, 结果为 $2
        >>[-]<<  # $2=0
        >>>[-]+<<<  # $3=true
        >>>[  # while $3
          [-]<<<  # $3=false

          [->>>>+>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<<  # 复制$0到$4 : $5
          > [->>>>+>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <  # 复制$1到$5 : $6

          >>>> ## 判断 $4<$5 开始
            # 缩进内$+4
            # 判断 $0<$1
            [->>+>+<<<]>>>[-<<<+>>>]<<<  # 复制$0到$2
            > [->>+>+<<<]>>>[-<<<+>>>]<<< <  # 复制$1到$3
            >>>>[-]>[-]+>[-]>[-]<<<<<<<  # $4=0, $5=1, $6=0, $7=0
            >>>>> [  # while $5
              [-] <<<<<  # $5=0

              >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
              >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0

              >>>>>>>[- <<<<<<< # while $7
                ## if $3: $5=$6=1
                >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
                >>>>[  # if $4
                  [-] # $4=0
                  >[-]+  # $5=1
                  >[-]+  # $6=1
                <<] <<<< # 判断复位$4

                ## if $2 && $3: $2--, $3-- 
                >>>>>>[  # if $6
                  [-]  # $6=0
                  <<<-  # $3--
                  <-  # $2--
                >>>>] <<<<<< # 判断复位$6

                >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
                >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0
                
                >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
                >>>>>>>>+<<<<[[-]>>>>-<<<<]<<<<  # $8=!$4, $4=0
                >>>>>>>> [[-]<[-]>] <<<<<<<<  # if $8: $7=$8=0

              >>>>>>>] <<<<<<<  # 循环复位$7
            >>>>>] <<<<<  # 循环复位$5

            >>>>[-]>[-]>[-]>[-]>[-]<<<<<<<<  # $4=$5=$6=$7=$8=0
            >>[-]>[[-]<+>]<<<  # 输出: $2 = ($0<$1)
            [-]>[-]>[-<<+>>]>[-]<<<  # $0=$2, $1=$2=$3=0
          <<<< ## 判断 $4<$5 结束, 结果为$4

          >>>> >+<[[-]>-<] <<<<  # $5=!$4=($0>=$1), $4=0
          >>>>> [-<+<+>>] <<<<<  # 复制$5到$3,$4, $5=0

          >>>>[  # if $4
            [-]<<<<  # $4=false
            > [->>>>+>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <  # 复制$1到$5 : $6
            >>>>> [-<<<<<->>>>>] <<<<<  # $0-=$5, $5=0
            >>+<<  # $2++
          >>>>] <<<< # 判断复位$4

        >>>] <<< # 循环复位$3
        # 计算结束, 商为 $2, 余为 $0
        [-]>[-]>[-<<+>>]<<  # $0=$2, $1=$2=0
      < ## 计算 $1 / $2 结束, 结果为 $1

      ## 判断$1非零,输出
      > [->+>+<<]>>[-<<+>>]<< <  # 复制$1到$2 : $3
      >> [[-]>+<] <<  # $3 = !!$2, $2=0
      >>>[  # if $3
        [-]<<<  # $3=false
        >>++++++[<++++++++>-]<<  # $1+=0x30
        >.[-]+<  # print $1, $1=true
      >>>] <<<  # 判断复位$3


      ## $2 存入十位, $3 存入个位
      [->>+>+>+<<<<]>>>>[-<<<<+>>>>]<<<<  # 复制$0到$2$3 : $4
      >>>> >++[<+++++>-]< <<<<  # $4=10

      >>> ## 计算 $3 % $4 开始
        # 缩进内 $+3
        # 计算 $0 % $1
        >>+<<  # $2=true
        >>[  # while $2
          [-]<<  # $2=false

          [->>>+>+<<<<]>>>>[-<<<<+>>>>]<<<<  # 复制$0到$3 : $4
          > [->>>+>+<<<<]>>>>[-<<<<+>>>>]<<<< <  # 复制$1到$4 : $5

          >>> # 判断 $3<$4 开始
            # 缩进内$+3
            [->>+>+<<<]>>>[-<<<+>>>]<<<  # 复制$0到$2
            > [->>+>+<<<]>>>[-<<<+>>>]<<< <  # 复制$1到$3
            >>>>[-]>[-]+>[-]>[-]<<<<<<<  # $4=0, $5=1, $6=0, $7=0
            >>>>> [  # while $5
            [-] <<<<<  # $5=0
            
            >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
            >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0
            
            >>>>>>>[- <<<<<<< # while $7
                ## if $3: $5=$6=1
                >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
                >>>>[  # if $4
                [-] # $4=0
                >[-]+  # $5=1
                >[-]+  # $6=1
                <<] <<<< # 判断复位$4
            
                ## if $2 && $3: $2--, $3-- 
                >>>>>>[  # if $6
                [-]  # $6=0
                <<<-  # $3--
                <-  # $2--
                >>>>] <<<<<< # 判断复位$6
            
                >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
                >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0
                
                >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
                >>>>>>>>+<<<<[[-]>>>>-<<<<]<<<<  # $8=!$4, $4=0
                >>>>>>>> [[-]<[-]>] <<<<<<<<  # if $8: $7=$8=0
            
            >>>>>>>] <<<<<<<  # 循环复位$7
            >>>>>] <<<<<  # 循环复位$5
            
            >>>>[-]>[-]>[-]>[-]>[-]<<<<<<<<  # $4=$5=$6=$7=$8=0
            >>[-]>[[-]<+>]<<<  # 输出: $2 = ($0<$1)
            
            # 处理返回值
            [-]>[-]<  # $0=$1=0
          >>[-<<<+>>>]<<  # 判断结果移动到 $2
          <<<  # 判断 $3<$4 结束

          >>>+<[[-]>-<]<<  # $3=!$2=($0>=$1), $2=0
          >>> [->+<<+>]>[-<+>]< <<<  # 复制$3到$2 : $4

          >>>[  # if $3 ($0>=$1)
            [-]<<<  # $3=false
            > [->>>+>+<<<<]>>>>[-<<<<+>>>>]<<<< <  # 复制$1到$4 : $5
            >>>>[-<<<<->>>>]<<<<  # $0-=$4, $4=0
          >>>] <<<  # 判断复位$3

        >>] <<  # 循环复位$2
        >[-]<  # $1=0, 取余结果为$0
      <<< ## 计算 $3 % $4 结束, 结果为 $3

      >>> [->+>+<<]>>[-<<+>>]<< <<<  # 复制$3到$4 : $5
      >>>> [-<<->>] <<<<  # $2-=$4, $4=0

      >> [->>+<<] <<  # $4=$2, $2=0
      >>>>> >++[<+++++>-]< <<<<<  # $5=10
      >>>> ## 计算 $4 / $5 开始
        # 缩进内 $+1
        # 计算 $0 / $1, 结果为 $2
        >>[-]<<  # $2=0
        >>>[-]+<<<  # $3=true
        >>>[  # while $3
          [-]<<<  # $3=false

          [->>>>+>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<<  # 复制$0到$4 : $5
          > [->>>>+>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <  # 复制$1到$5 : $6

          >>>> ## 判断 $4<$5 开始
            # 缩进内$+4
            # 判断 $0<$1
            [->>+>+<<<]>>>[-<<<+>>>]<<<  # 复制$0到$2
            > [->>+>+<<<]>>>[-<<<+>>>]<<< <  # 复制$1到$3
            >>>>[-]>[-]+>[-]>[-]<<<<<<<  # $4=0, $5=1, $6=0, $7=0
            >>>>> [  # while $5
              [-] <<<<<  # $5=0

              >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
              >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0

              >>>>>>>[- <<<<<<< # while $7
                ## if $3: $5=$6=1
                >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
                >>>>[  # if $4
                  [-] # $4=0
                  >[-]+  # $5=1
                  >[-]+  # $6=1
                <<] <<<< # 判断复位$4

                ## if $2 && $3: $2--, $3-- 
                >>>>>>[  # if $6
                  [-]  # $6=0
                  <<<-  # $3--
                  <-  # $2--
                >>>>] <<<<<< # 判断复位$6

                >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
                >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0
                
                >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
                >>>>>>>>+<<<<[[-]>>>>-<<<<]<<<<  # $8=!$4, $4=0
                >>>>>>>> [[-]<[-]>] <<<<<<<<  # if $8: $7=$8=0

              >>>>>>>] <<<<<<<  # 循环复位$7
            >>>>>] <<<<<  # 循环复位$5

            >>>>[-]>[-]>[-]>[-]>[-]<<<<<<<<  # $4=$5=$6=$7=$8=0
            >>[-]>[[-]<+>]<<<  # 输出: $2 = ($0<$1)
            [-]>[-]>[-<<+>>]>[-]<<<  # $0=$2, $1=$2=$3=0
          <<<< ## 判断 $4<$5 结束, 结果为$4

          >>>> >+<[[-]>-<] <<<<  # $5=!$4=($0>=$1), $4=0
          >>>>> [-<+<+>>] <<<<<  # 复制$5到$3,$4, $5=0

          >>>>[  # if $4
            [-]<<<<  # $4=false
            > [->>>>+>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <  # 复制$1到$5 : $6
            >>>>> [-<<<<<->>>>>] <<<<<  # $0-=$5, $5=0
            >>+<<  # $2++
          >>>>] <<<< # 判断复位$4

        >>>] <<< # 循环复位$3
        # 计算结束, 商为 $2, 余为 $0
        [-]>[-]>[-<<+>>]<<  # $0=$2, $1=$2=0
      <<<< ## 计算 $4 / $5 结束, 结果为 $4

      >>>>> >++[<+++++>-]< <<<<<  # $5=10
      >>>> ## 计算 $4 % $5 开始
        # 缩进内 $+3
        >>[-]<<  # $2=0
        >>>[-]+<<<  # $3=true
        >>>[  # while $3
          [-]<<<  # $3=false

          [->>>>+>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<<  # 复制$0到$4 : $5
          > [->>>>+>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <  # 复制$1到$5 : $6

          >>>> ## 判断 $4<$5 开始
            # 缩进内$+4
            # 判断 $0<$1
            [->>+>+<<<]>>>[-<<<+>>>]<<<  # 复制$0到$2
            > [->>+>+<<<]>>>[-<<<+>>>]<<< <  # 复制$1到$3
            >>>>[-]>[-]+>[-]>[-]<<<<<<<  # $4=0, $5=1, $6=0, $7=0
            >>>>> [  # while $5
              [-] <<<<<  # $5=0

              >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
              >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0

              >>>>>>>[- <<<<<<< # while $7
                ## if $3: $5=$6=1
                >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
                >>>>[  # if $4
                  [-] # $4=0
                  >[-]+  # $5=1
                  >[-]+  # $6=1
                <<] <<<< # 判断复位$4

                ## if $2 && $3: $2--, $3-- 
                >>>>>>[  # if $6
                  [-]  # $6=0
                  <<<-  # $3--
                  <-  # $2--
                >>>>] <<<<<< # 判断复位$6

                >> [->>>>>+>+<<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<  # 复制$2到$8 : $7
                >>>>>>>> [[-]<+>] <<<<<<<<  # $7=!!$8, $8=0
                
                >>> [->+>>>>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <<<  # 复制$3到$4 : $8
                >>>>>>>>+<<<<[[-]>>>>-<<<<]<<<<  # $8=!$4, $4=0
                >>>>>>>> [[-]<[-]>] <<<<<<<<  # if $8: $7=$8=0

              >>>>>>>] <<<<<<<  # 循环复位$7
            >>>>>] <<<<<  # 循环复位$5

            >>>>[-]>[-]>[-]>[-]>[-]<<<<<<<<  # $4=$5=$6=$7=$8=0
            >>[-]>[[-]<+>]<<<  # 输出: $2 = ($0<$1)
            [-]>[-]>[-<<+>>]>[-]<<<  # $0=$2, $1=$2=$3=0
          <<<< ## 判断 $4<$5 结束, 结果为$4

          >>>> >+<[[-]>-<] <<<<  # $5=!$4=($0>=$1), $4=0
          >>>>> [-<+<+>>] <<<<<  # 复制$5到$3,$4, $5=0

          >>>>[  # if $4
            [-]<<<<  # $4=false
            > [->>>>+>+<<<<<]>>>>>[-<<<<<+>>>>>]<<<<< <  # 复制$1到$5 : $6
            >>>>> [-<<<<<->>>>>] <<<<<  # $0-=$5, $5=0
            >>+<<  # $2++
          >>>>] <<<< # 判断复位$4

        >>>] <<< # 循环复位$3
        # 计算结束, 商为 $2, 余为 $0
        >[-]>[-]>[-]<<<  # $1=$2=$3=0
      <<<< ## 计算 $4 % $5 结束, 结果为 $4
      >>>> [-<<+>>] <<<< # $2=$4, $4=0

      ## 判断$2非零或$1非零,输出
      >> [->>+>+<<<]>>>[-<<<+>>>]<<< <<  # 复制$2到$4 : $5
      >>>> [[-]>+<] <<<<  # $5 = !!$4, $4=0
      > [[-]>>>>+<<<<]+ <  # $5 |= $1, $1=true
      >>>>>[  # if $5
        [-]<<<<<  # $5=false
        >> >>++++++[<<++++++++>>-]<< <<  # $2+=0x30
        >>.[-]+<<  # print $2, $2=true
      >>>>>] <<<<<  # 判断复位$5

      ## 判断$3非零或$2非零或$1非零,输出
      >>> [->+>+<<]>>[-<<+>>]<< <<<  # 复制$3到$4 : $5
      >>>> [[-]>+<] <<<<  # $5 = !!$4, $4=0
      > [[-]>>>>+<<<<]+ <  # $5 |= $1, $1=true
      >> [[-]>>>+<<<]+ <<  # $5 |= $2, $2=true
      >>>>>[  # if $5
        [-]<<<<<  # $5=false
        >>> >++++++[<++++++++>-]< <<<  # $3+=0x30
        >>>.[-]<<<  # print $3, $3=0
      >>>>>] <<<<<  # 判断复位$5
      [-]>[-]>[-]>[-]>[-]>[-]<<<<<  # 清空
    <<< # 输出数值 $3 结束

    >> >++++[<++++++++>-]<.[-] <<  # 输出0x20(空格)
  >>] <<  # 判断复位$2

] <  # 循环复位$0

%
